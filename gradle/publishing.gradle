apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'com.gradle.plugin-publish'

ext {
    bintrayUser = resolveProperty('BINTRAY_USER', 'bintrayUser')
    bintrayKey = resolveProperty('BINTRAY_KEY', 'bintrayKey')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact groovydocJar
            artifact javadocJar
        }
    }
}

artifactoryPublish {
    onlyIf { isMaster }
    mustRunAfter 'build'
    publications 'mavenJava'
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'

    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = bintrayUser
            password = bintrayKey
            maven = true
        }
    }
}

pluginBundle {
    website = 'https://github.com/node-gradle/gradle-node-plugin'
    vcsUrl = 'https://github.com/node-gradle/gradle-node-plugin'

    plugins {
        nodePlugin {
            id = 'com.github.node-gradle.node'
            displayName = 'Gradle Node Plugin'
            description = 'Gradle plugin for executing node scripts.'
            tags = ['java', 'gradle', 'node', 'yarn', 'npm']
        }
    }

    mavenCoordinates {
        groupId = 'com.github.node-gradle'
        artifactId = 'gradle-node-plugin'
    }
}

String resolveProperty(String envVarKey, String projectPropKey) {
    String propValue = System.getenv()[envVarKey]

    if (propValue) {
        return propValue
    }

    hasProperty(projectPropKey) ? getProperty(projectPropKey) : null
}
