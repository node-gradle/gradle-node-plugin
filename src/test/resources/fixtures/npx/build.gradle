import org.gradle.util.GradleVersion

plugins {
    id "com.github.node-gradle.node"
}

node {
    npmVersion = "8.5.0"
    download = true
    workDir = file("build/node")
}

// mocha is installed locally whereas eslint is not

task lint(type: NpxTask) {
    dependsOn npmInstall
    command = "eslint@6.3.0"
    args = ["src", "test"]
    inputs.file(".eslintrc.yml")
    inputs.dir("src")
    inputs.dir("test")
    outputs.upToDateWhen {
        true
    }
}

task test(type: NpxTask) {
    dependsOn lint
    command = "mocha"
    inputs.file("package.json")
    inputs.dir("src")
    inputs.dir("test")
    outputs.upToDateWhen {
        true
    }
}

task env(type: NpxTask) {
    command = "print-env"
    outputs.upToDateWhen {
        true
    }
}

task cwd(type: NpxTask) {
    command = "cwd"
    outputs.upToDateWhen {
        true
    }
}

task version(type: NpxTask) {
    dependsOn npmInstall
    command = "--version"
}

if (isPropertyEnabled("changeInputs")) {
    lint.args = ["src"]
    test.command = "_mocha"
}

def isPropertyEnabled(String name) {
    def provider = providers.systemProperty(name)
    if (org.gradle.util.GradleVersion.current() >= org.gradle.util.GradleVersion.version("7.4")) {
        return provider.isPresent()
    } else {
        return provider.forUseAtConfigurationTime().isPresent()
    }
}
