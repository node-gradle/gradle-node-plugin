import org.gradle.util.GradleVersion

plugins {
    id "com.github.node-gradle.node"
}

node {
    download = true
    workDir = file("build/node")
}

task env(type: YarnTask) {
    dependsOn yarn
    yarnCommand = ["run", "print-env"]
    outputs.upToDateWhen {
        true
    }
}

// This should be documented in the package.json file but it cannot be done because JSON does not accept comments.
// npm run forces the current directory but we can read the INIT_CWD environment variable to get the original working
// directory. This is the reason why we read the INIT_CWD variable and not the process.cwd() one in the script
// definition.
task pwd(type: YarnTask) {
    dependsOn yarn
    yarnCommand = ["run", "printcwd"]
    outputs.upToDateWhen {
        true
    }
}

task version(type: YarnTask) {
    dependsOn yarn
    yarnCommand = ["--version"]
}

if (isPropertyEnabled("customEnv")) {
    def qualifier = "custom"
    env.environment = [CUSTOM: "${qualifier} value"]
}

if (isPropertyEnabled("ignoreExitValue")) {
    env.ignoreExitValue = true
}

if (isPropertyEnabled("notExistingCommand")) {
    env.yarnCommand = ["notExistingCommand"]
}

if (isPropertyEnabled("customWorkingDir")) {
    pwd.workingDir = file("${project.buildDir}/customWorkingDirectory/")
}

if (isPropertyEnabled("outputFile")) {
    env.execOverrides {
        standardOutput = new FileOutputStream("${buildDir}/standard-output.txt")
    }
}

if (isPropertyEnabled("enableHooks")) {
    env.hooks {
        onSuccess {
            println("Env task success with status ${it.getExitValue()}")
        }
        onFailure {
            println("Env task failure with status ${it.getExitValue()}")
        }
    }
}

def isPropertyEnabled(String name) {
    if (GradleVersion.current() >= GradleVersion.version("6.6")) {
        return providers.systemProperty(name).forUseAtConfigurationTime().isPresent()
    }
    return System.properties[name] != null
}
