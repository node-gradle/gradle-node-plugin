import org.gradle.util.GradleVersion

plugins {
    id 'com.github.node-gradle.node'
}

def changeInputs = isPropertyEnabled("changeInputs")

node {
    npmVersion = "8.1.0"
    download = true
    workDir = file('build/node')
}

task test(type: NpmTask) {
    dependsOn npmInstall
    npmCommand = changeInputs ? ['run', 'test'] : ['run']
    args = changeInputs ? [] : ['test']
    inputs.file('package.json')
    inputs.dir('src')
    inputs.dir('test')
    outputs.upToDateWhen {
        true
    }
}

task version(type: NpmTask) {
    dependsOn npmInstall
    npmCommand = ["--version"]
}

def isPropertyEnabled(String name) {
    if (GradleVersion.current() >= GradleVersion.version("6.6")) {
        return providers.systemProperty(name).forUseAtConfigurationTime().isPresent()
    }
    return System.properties[name] != null
}
