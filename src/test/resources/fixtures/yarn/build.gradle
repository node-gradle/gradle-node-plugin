import org.gradle.util.GradleVersion

plugins {
    id "com.github.node-gradle.node"
}

def changeInputs = isPropertyEnabled("changeInputs")

node {
    yarnVersion = "1.22.17"
    download = true
    workDir = file("build/node")
}

task test(type: YarnTask) {
    dependsOn yarn
    yarnCommand = changeInputs ? ["run", "test"] : ["run"]
    args = changeInputs ? [] : ["test"]
    inputs.dir("node_modules")
    inputs.file("package.json")
    inputs.dir("src")
    inputs.dir("test")
    outputs.upToDateWhen {
        true
    }
}

task version(type: YarnTask) {
    dependsOn yarn
    yarnCommand = ["--version"]
}

def isPropertyEnabled(String name) {
    if (GradleVersion.current() >= GradleVersion.version("6.6")) {
        return providers.systemProperty(name).forUseAtConfigurationTime().isPresent()
    }
    return System.properties[name] != null
}
