import org.gradle.util.GradleVersion

plugins {
    id "com.github.node-gradle.node"
}

node {
    download = true
    workDir = file("build/node")
}

def changeOptions = isPropertyEnabled("changeOptions")

def changeEnv = isPropertyEnabled("changeEnv")
def changeWorkingDir = isPropertyEnabled("changeWorkingDir")
def fail = isPropertyEnabled("fail")
def ignoreExitValue = isPropertyEnabled("ignoreExitValue")
def outputFile = isPropertyEnabled("outputFile")
def enableHooks = isPropertyEnabled("enableHooks")

task env(type: NodeTask) {
    script = file("env.js")
    outputs.upToDateWhen {
        true
    }
}

if (changeOptions) {
    env.options = ["--print", "1000 * 1000"]
}

if (changeEnv) {
    def qualifier = "custom"
    env.environment = [CUSTOM: "${qualifier} value"]
}

if (changeWorkingDir) {
    env.workingDir = file("${project.buildDir}/notExisting")
}

if (fail) {
    env.args = ['fail']
}

if (ignoreExitValue) {
    env.ignoreExitValue = true
}

if (outputFile) {
    env.execOverrides {
        standardOutput = new FileOutputStream("${buildDir}/standard-output.txt")
    }
}

if (enableHooks) {
    env.hooks {
        onSuccess {
            println("Env task success with status ${it.getExitValue()}")
        }
        onFailure {
            println("Env task failure with status ${it.getExitValue()}")
        }
    }
}

task version(type: NodeTask) {
    script = file("version.js")
}

def isPropertyEnabled(String name) {
    if (GradleVersion.current() >= GradleVersion.version("6.6")) {
        return providers.systemProperty(name).forUseAtConfigurationTime().isPresent()
    }
    return System.properties[name] != null
}
